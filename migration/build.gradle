/*
 * Copyright 2019-2025 Chemaxon Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'java'
    id 'application'
}

checkProjectProperties()

repositories {
    maven {
        url = "${chemaxonRepositoryUrl}/libs-release"
        credentials {
            username = chemaxonRepositoryUser
            password = chemaxonRepositoryPassword
        }
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation "com.chemaxon:io-all:${chemaxonReleaseVersion}"

    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
}

tasks.register('transferData', JavaExec) {
    group = 'transferdata'
    description = 'Transfers data from molecule files to json for batch insert in JChem Microservices'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.chemaxon.example.TransferData'
    args loadFilesFromFolder()
}

def loadFilesFromFolder() {
    def inputDir = file('./input')
    if (!inputDir.exists() || !inputDir.isDirectory() || !(inputDir.listFiles())) {
        logger.warn('Input folder is not available')
        return []
    }
    def files = file('./input').listFiles().sort()
    def names = files.findAll { File file -> file.isFile() && file.name != '.gitkeep' }.collect { it.absolutePath }
    logger.info("These files will be used: ${names}")
    return names
}

private void checkProjectProperties() {
    def missing = List.of('chemaxonRepositoryUser', 'chemaxonRepositoryPassword').findAll { !project.hasProperty(it) || !project[it] }
    if (!missing.isEmpty()) {
        throw new GradleException("""The following settings are missing: ${missing}
Please follow the guide here: https://docs.chemaxon.com/display/docs/general_public-repository.md
and add your email address and identity token to the gradle.properties file.
""")
    }
}
