/*
 * Copyright 2019-2025 Chemaxon Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id 'java-library'
}

checkProjectProperties()

repositories {
    maven {
        url = 'https://chemaxon.jfrog.io/artifactory/libs-release'
        credentials {
            username = chemaxonRepositoryUser
            password = chemaxonRepositoryPassword
        }
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation "com.chemaxon:structurechecker:${chemaxonReleaseVersion}"
    implementation "com.chemaxon:io-all:${chemaxonReleaseVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    // Use JUnit platform for unit tests
    useJUnitPlatform()
}

private void checkProjectProperties() {
    def missing = List.of('chemaxonRepositoryUser', 'chemaxonRepositoryPassword').findAll { !project.hasProperty(it) || !project[it] }
    if (!missing.isEmpty()) {
        throw new GradleException("""The following settings are missing: ${missing}
Please follow the guide here: https://docs.chemaxon.com/display/docs/general_public-repository.md
and add your email address and identity token to the gradle.properties file.
""")
    }
}
